
group projectGroup
version projectVersion

apply plugin: 'java'

Properties env = new Properties()
env.load(new FileInputStream("env.properties"))
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

testSets {
    integrationTest { dirName = 'integration-test' }
    functionalTest { dirName = 'functional-test' }
}

test.outputs.upToDateWhen {false}
integrationTest.outputs.upToDateWhen {false}
functionalTest.outputs.upToDateWhen {false}

sourceSets {
    functionalTest {
        java {
            srcDir 'src/main/java'
        }
    }
    integrationTest {
        java {
            srcDir 'src/main/java'
        }
    }
    main {
        resources {
            srcDirs = ["src/main/java", "src/main/resources"]
            includes = ["**/*.fxml", "**/*.xml", "**/*.properties", "**/*.css", "**/*.json", "**/*.sql",
                        "**/*.png", "**/*.gif", "**/*.jpg", "**/*.ico", "**/*.html", "**/*.js", "**/*.jrxml"]
        }
    }
}

bintray {
    user = bintrayUser
    key = env.getProperty("BINTRAY_APIKEY")
    publish = true
    pkg {
        repo = bintrayRepository
        name = projectArtifact
        desc = appDescription
        userOrg = bintrayUser
        licenses = ((String) projectLicenses).split(";")
        vcsUrl = appScm
        labels = ((String) projectTags).split(";")
        publicDownloadNumbers = true
        websiteUrl = appUrl
    }
    publications = ['appPublication']
}

publishing {
    publications {
        appPublication(MavenPublication) {
            from components.java
            groupId projectGroup
            artifactId projectArtifact
            version projectVersion
        }
    }
}


task publishAppBintray(dependsOn: ['clean', 'test', 'integrationTest', 'functionalTest', 'bintrayUpload']){
    test.mustRunAfter clean
    integrationTest.mustRunAfter test
    functionalTest.mustRunAfter integrationTest
    bintrayUpload.mustRunAfter functionalTest
}